plugins {
    id 'java'
}

group = 'org.dustyroom'
version = '0.4.4'

repositories {
    mavenCentral()
}

dependencies {
    def lombokVersion = '1.18.30'

    implementation 'ch.qos.logback:logback-classic:1.4.12'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.dustyroom.Main',
        )
    }
}

tasks.register('fatJar', Jar) {
    manifest.from jar.manifest
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

artifacts {
    archives fatJar
}

test {
    useJUnitPlatform()
}